{
    DYOM Visuals
    ------------
    A DYOM Add-on to add custom shaders and special effects to DYOM missions and storylines
    
    Created by:
        Toriality
        Miran
        
    Special thanks to:
        Dust Rathard
    
    Current version:
        v1.0.0
        
    Date of release (current version):
        <...>
    
    Date of release (first version):
        <...>
        
    Date of development start:
        March 23, 2023
    
    Useful links:
        <...>    
}

{$CLEO .cs}
{$USE ini}
{$USE bitwise}
{$NOSOURCE} // do not make script bigger


const
    hShader = 10@
    forceBumper = 11@
    forceWalking = 12@
    forceAiming = 13@
    previewMode = 14@
    lastSelected = 15@
    shaderDone = 16@ // done or still need peer frame updates
    nextFrameCount = 17@
    wasGamePaused = 18@
    
    previewMode_off = 0
    previewMode_editor = 1
    previewMode_objective = 2
end

var
    hShader: shader
    forceAiming: int
    forceWalking: int
    forceBumper: int
    previewMode: int
    lastSelected: int
    nextFrameCount: int
    wasGamePaused: int
end

script_name "DVISUALS"

{$INCLUDE shaderApi}
{$INCLUDE DYOM_Addon_Base.txt}
{$INCLUDE DYOM_Visuals_Config.txt}
{$INCLUDE DYOM_Visuals_Shaders.txt} 
{$INCLUDE DYOM_Visuals_Effects.txt}


:DYOM_INIT
    previewMode = previewMode_off
    lastSelected = -1
    wasGamePaused = false
    
    SHADERS_INIT()
    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader -2 result shaderDone // set default parameters
return                      
 
     
:DYOM_MISSION_STARTED
    TimerA = 0 // mission time
return


:DYOM_MISSION_ENDED
    previewMode = previewMode_off
    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader -2 result shaderDone // set default parameters
    
    0@ = -2 // restore defaults
    EFFECTS_CONFIG_UPDATE_PARAMS()
return


:DYOM_OBJECTIVE_STARTED 
    TimerB = 0 // objective time
    
    0@ += 1 // 1-based objective index
    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader 0@ result shaderDone // current objective
    
    // current objective in 0@
    EFFECTS_CONFIG_UPDATE_PARAMS()
return


:DYOM_OBJECTIVE_ENDED
    015D: set_gamespeed 1.0 // clean up speed changes
return


:DYOM_RUN // editor and mission    
    SHADERS_EDITOR_UPDATE() // handle keys etc.
    cleo_call @SHADERS_CONSTANTS_UPDATE args 1 hShader
    cleo_call @SHADERS_DRAW args 1 hShader
return


:DYOM_MISSION_RUN
    if
        shaderDone == false // still needs updates
    then
        008B: 0@ = dyom_objective_index
        0@ += 1 // 1-based objective index
        cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader 0@ result shaderDone // current objective
    end
      
    EFFECTS_PROCESS()
return


:SHADERS_EDITOR_UPDATE    
    if
        $ONMISSION == 0 // editor mode
    then
        0@ = false // reload ini file
    
        // FPS Count to check whether game was paused/alt-tabbed or not
        0A8D: 1@ = read_memory 0xb7cb4c size 4 virtual_protect 0
        if
            1@ > nextFrameCount
        then
            0@ = true // game was paused, reload ini file
        end 
        nextFrameCount = 1@
        nextFrameCount += 1
    
        // handle preview mode keyboard key
        
        if
            is_key_pressed 0x7B // F12
            //0736: is_keyboard_key_just_pressed 0x7B // F12
        then
            if
                is_key_pressed 0x11 // CTRL
            then
                SHADERS_INIT() // rebuild shaders
            else
                previewMode += 1
                
                if
                    previewMode == previewmode_editor
                then
                    0ACE: show_formatted_text_box "Effects: ~y~~h~Editor~s~"
                else
                    if
                        previewMode == previewmode_objective
                    then
                        0ACE: show_formatted_text_box "Effects: ~g~~h~Objective~s~"
                    else
                        previewMode = previewMode_off
                        0ACE: show_formatted_text_box "Effects: ~r~~h~OFF~s~"    
                    end
                end
            end
            
            // wait for mode key released
            while is_key_pressed 0x7B // F12
                wait 0
            end
            
            0@ = true // reload ini file
        end
        
        // handle editor selected objective changes
        if and
            previewMode == previewmode_objective
            87D6: lastSelected <> $15617
        then
            008B: lastSelected = $15617
            0@ = true // reload ini file
        end
        
        // reload config from .ini file
        if
            0@ == true // update requested
        then
            if
                previewMode == previewmode_off    
            then
                cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader -2 result shaderDone // set default parameters
                //0AD1: show_formatted_text_highpriority "~g~~h~OK:~s~ Effects disabled" time 2000
            end
            
            if
                previewMode == previewmode_editor // use editor.ini
            then
                set_current_directory 0 // game root
                if
                    does_file_exist "cleo/dyom_shaders/editor.ini"
                then
                    0AD1: show_formatted_text_highpriority "~g~~h~OK:~s~ Loaded shaders from editor.ini" time 2000
                    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader -1 result shaderDone // editor.ini
                else
                    0AD1: show_formatted_text_highpriority "~r~~h~ERROR:~s~ File cleo\dyom_shaders\editor.ini not found!" time 10000
                    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader -2 result shaderDone // restore defaults    
                end
            end
            
            if
                previewMode == previewmode_objective // use mission's visuals.ini
            then
                if
                    cleo_call @CONFIG_EXISTS args 0
                then
                    0AD1: show_formatted_text_highpriority "~g~~h~OK:~s~ Loaded shaders for Objective %d" time 3000 lastSelected
                    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader lastSelected result shaderDone
                else      
                    cleo_call @CONFIG_GET_FILEPATH args 0 result 1@ 2@ 3@ 4@   
                    0AD1: show_formatted_text_highpriority "~r~~h~ERROR:~s~ Visuals file for this mission not found!~n~Please create %s" time 10000 1@v            
                    cleo_call @SHADERS_CONFIG_UPDATE_PARAMS 2 hShader -2 result shaderDone // restore defaults  
                end
            end  
        end
    end
return
